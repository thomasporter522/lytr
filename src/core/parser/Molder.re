open Stds;

// replace ghost with piece above bridge
// let x = 1 >in< x + 1
// let x = 1 >in< x + 1 [in]
// let x = 1 >< x + 1 [in]
// let x = 1 >< x + 1 in <>

// replace ghost with piece under bridge
// let x = 1 + 2 >in< x + 1
// let x = 1 [in] + 2 >in< x + 1
//
// let x = 1 in <> + 2 >< x + 1

// replacing even solid bridges?
// let x = 1 + 2 in x + 1
// let x = 1 [in] + 2 in x + 1
//
// let x = 1 in <> + 2 >in< x + 1
// or
// let x = 1 in <> + 2 >< <in> >< x + 1

let debug = ref(false);

let candidates =
    (~re: option(Mtrl.T.t)=?, t: Token.Unmolded.t): list(Token.t) =>
  List.map(
    Token.mk(~id=t.id, ~marks=?t.marks, ~text=t.text),
    switch (t.mtrl) {
    | Space(t) => [Mtrl.Space(t)]
    | Grout(_) => []
    | Tile(lbls) =>
      lbls
      |> List.concat_map(lbl =>
           Molds.with_label(lbl) |> List.map(mold => (lbl, mold))
         )
      |> List.stable_sort(
           ((lbl_l, m_l: Mold.t) as l, (lbl_r, m_r: Mold.t) as r) => {
           open Compare.Syntax;
           let/ () =
             switch (re) {
             | Some(Tile(t)) when t == l => (-1)
             | Some(Tile(t)) when t == r => 1
             | _ => 0
             };
           let/ () = Sort.compare(m_l.sort, m_r.sort);
           (-1)
           * Bool.compare(
               Label.is_complete(t.text, lbl_l),
               Label.is_complete(t.text, lbl_r),
             );
         })
      |> List.map(Mtrl.tile)
    },
  );

let complete_pending_ghosts = (~bounds, l: Stack.t, ~fill) => {
  let (grouted, effs) =
    Effects.dry_run(
      () => Melder.complete_bounded(~bounds, ~onto=L, l.slope, ~fill),
      (),
    );
  // hack(?) to avoid completion if no new ghosts are generated. if only
  // grout are generated, then we can generate them later as needed at the
  // end of remolding. better to delay their generation bc there may already
  // be grout in the suffix whose relative position around neighboring
  // whitespace we want to preserve.
  effs
  |> List.for_all(
       fun
       | Effects.Insert(tok) =>
         Mtrl.(is_grout(tok.mtrl) || is_space(tok.mtrl))
       | Remove(_) => true,
     )
    ? (l, fill)
    : {
      Effects.commit(effs);
      let ((_, cell), stack) = Stack.connect_(grouted, {...l, slope: []});
      let (fill, slope) = Slope.Dn.unroll(cell);
      (Stack.cat(slope, stack), fill);
    };
};

// whether a token is redundant and can be removed given the result of melding
let is_redundant = (tok: Token.t, grouted: Grouted.t, stack: Stack.t) => {
  Mtrl.is_tile(tok.mtrl)
  && Token.is_empty(tok)
  && (
    Token.is_complete(tok)
    || Grouted.is_neq(grouted)
    || Option.is_some(Grouted.is_eq(grouted))
    && (
      stack.slope == []
      || (
        // hack to clean up ghosts with starred ctxs in hazel
        switch (
          stack |> Stack.face(~from=L) |> Bound.map(Token.mtrl),
          tok.mtrl,
        ) {
        | (
            Node(Tile((Const(_, _, ","), _))),
            Tile((Const(_, _, ","), _)),
          ) =>
          true
        | (
            Node(Tile((Const(_, _, "=>"), m_l))),
            Tile((Const(_, _, "|"), m_r)),
          ) =>
          m_l.prec == m_r.prec
        | (
            Node(Tile((Const(_, _, "=>"), m_l))),
            Tile((Const(_, _, "=>"), m_r)),
          ) =>
          m_l.prec == m_r.prec && m_l.prec == 2
        | _ => false
        }
      )
    )
  );
};

// returns Error(fill) if input token is empty
// re indicates whether token is being remolded
let rec mold =
        (~re=?, stack: Stack.t, ~fill=Cell.empty, t: Token.Unmolded.t)
        : Result.t((Token.t, Grouted.t, Stack.t), Cell.t) => {
  // if (debug^) {
  //   P.log("--- Molder.mold");
  //   P.show("re", string_of_bool(re));
  //   P.show("stack", Stack.show(stack));
  //   P.show("fill", Cell.show(fill));
  //   P.show("t", Token.Unmolded.show(t));
  // };
  switch (
    candidates(~re?, t)
    |> Oblig.Delta.minimize(tok => {
         Melder.push(
           ~no_eq=Option.is_some(re),
           tok,
           ~fill,
           stack,
           ~onto=L,
           ~repair=remold,
         )
         |> Option.map(((grouted, stack)) => (tok, grouted, stack))
       })
  ) {
  | Some((tok, grouted, stack) as molded) =>
    // remove empty ghost connected via neq-relation
    // P.log("--- Molder.mold/success");
    // P.show("tok", Token.show(tok));
    // P.show("grouted", Grouted.show(grouted));
    // P.show("stack", Stack.show(stack));
    is_redundant(tok, grouted, stack)
      ? Error(Cell.mark_degrouted(fill, ~side=R)) : Ok(molded)
  | None =>
    // P.log("--- Molder.mold/deferring");
    let deferred = Token.Unmolded.defer(t);
    Token.is_empty(deferred)
      ? Error(Cell.mark_degrouted(fill, ~side=R))
      : Ok(
          {
            let (fill, slope) = Slope.Dn.unroll(fill);
            let stack = Stack.cat(slope, stack);
            Melder.push(
              ~no_eq=Option.is_some(re),
              deferred,
              ~fill,
              stack,
              ~onto=L,
            )
            |> Option.map(((grouted, stack)) => (deferred, grouted, stack))
            |> Options.get_fail("bug: failed to push space");
          },
        );
  };
}
// returns Ok if all suffix elements are remolded by the prefix without changing
// stack bound. returns Error if a suffix element changes stack bound when remolded
// and returns remaining suffix elements.
and remold =
    (~fill, (l, r): Stack.Frame.t)
    : Result.t((Slope.Dn.t, Cell.t), (Cell.t, Stack.Frame.t)) => {
  // P.log("--- Molder.remold");
  // P.show("fill", Cell.show(fill));
  // P.show("(l, r)", Stack.Frame.show((l, r)));
  let bounds = (l.bound, r.bound);
  switch (r.slope) {
  | [] =>
    // P.log("--- Molder.remold/done");
    // P.show("l", Stack.show(l));
    // P.show("fill", Cell.show(fill));
    Ok((l.slope, fill))
  | [hd, ...tl] =>
    // P.log("--- Molder.remold/continue");
    // P.show("hd", Terr.show(hd));
    // insert any pending ghosts if next terr has newlines
    let (l, fill) =
      Terr.tokens(hd)
      |> List.map(Token.height)
      |> List.fold_left((+), 0) == 0
        ? (l, fill) : complete_pending_ghosts(~bounds, l, ~fill);
    let r_tl = {...r, slope: tl};
    let (hd_w, tl_w) = Wald.uncons(hd.wald);
    // tl_w unrolled to up slope
    let unroll_tl_w_hd_cell = () =>
      Chain.Affix.uncons(tl_w)
      |> Option.map(((cell, (ts, cs))) => {
           let (c, up) = Slope.Up.unroll(cell);
           let up = up @ [{wald: Wald.mk(ts, cs), cell: hd.cell}];
           (c, up);
         })
      |> Option.value(~default=Slope.Up.unroll(hd.cell));
    // P.log("--- Molder.remold/continue/molding");
    // P.show("l", Stack.show(l));
    // P.show("fill", Cell.show(fill));
    // P.show("hd_w", Token.show(hd_w));
    switch (mold(~re=hd_w.mtrl, l, ~fill, Labeler.unmold(hd_w))) {
    | Error(fill) =>
      // P.log("--- Molder.remold/continue/molding/error");
      // P.show("fill", Cell.show(fill));
      Effects.remove(hd_w);
      let (c, up) = unroll_tl_w_hd_cell();
      let fill = fill |> Cell.pad(~r=c) |> Cell.mark_ends_dirty;
      (l, r_tl) |> Stack.Frame.cat(([], up)) |> remold(~fill);
    | Ok((t, grouted, rest)) when t.mtrl == hd_w.mtrl =>
      // fast path for when hd_w retains original meld
      // P.log("--- Molder.remold/continue/molding/fast path");
      // P.show("t", Token.show(t));
      // P.show("grouted", Grouted.show(grouted));
      // P.show("rest", Stack.show(rest));
      let connected = Stack.connect(t, grouted, rest) |> Stack.extend(tl_w);
      let fill = Cell.mark_ends_dirty(hd.cell);
      let (fill, r_tl) =
        // unroll hd terrace cell if it contains an unmolded obligation,
        // in case it can be molded by the new prefix
        if (Path.Map.exists(
              _ =>
                fun
                | Mtrl.Space(Space.T.Unmolded) => true
                | _ => false,
              fill.marks.obligs,
            )) {
          let (fill, unrolled) = Slope.Up.unroll(fill);
          (fill, Stack.cat(unrolled, r_tl));
        } else {
          (fill, r_tl);
        };
      if (connected.bound == l.bound) {
        remold(~fill, (connected, r_tl));
      } else {
        Error((fill, (connected, r_tl)));
      };
    | Ok((t, grouted, rest)) =>
      // P.log("--- Molder.remold/continue/molding/default path");
      // P.show("t", Token.show(t));
      // P.show("grouted", Grouted.show(grouted));
      // P.show("rest", Stack.show(rest));
      Effects.remove(hd_w);
      let connected = Stack.connect(Effects.insert(t), grouted, rest);
      // check if connection changed the stack bound
      if (connected.bound == l.bound) {
        // if not, then nearest bidelimited container is preserved
        let (fill, up) = unroll_tl_w_hd_cell();
        (connected, r_tl)
        |> Stack.Frame.cat(([], up))
        |> remold(~fill=Cell.mark_ends_dirty(fill));
      } else {
        Error((Cell.mark_ends_dirty(hd.cell), (connected, r_tl)));
      };
    };
  };
};
